plugins {
    id 'java'
    id 'application'
}

group = "com.cubiclauncher"
version = "2501a"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

def fxVersion = "21.0.4"

def os = org.gradle.internal.os.OperatingSystem.current()
def platform = os.isWindows() ? "win" :
        os.isLinux()   ? "linux" :
                os.isMacOsX()  ? "mac" : null
def arch = System.getProperty("os.arch").toLowerCase()
def normalizedArch = arch.contains("64") ? "x64" :
        arch.contains("arm") || arch.contains("aarch64") ? "aarch64" :
                "x86"
if (platform == null) {
    throw new GradleException("Unsupported platform for JavaFX")
}

dependencies {
    implementation "org.openjfx:javafx-base:${fxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${fxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${fxVersion}:${platform}"
    implementation "org.openjfx:javafx-fxml:${fxVersion}:${platform}"

    implementation "org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0"
    implementation "com.google.code.gson:gson:2.13.2"
    implementation "com.cubiclauncher:claunch:1.0.0"
    implementation "net.java.dev.jna:jna:5.18.1"
    implementation "net.java.dev.jna:jna-platform:5.18.1"
    implementation "ch.qos.logback:logback-classic:1.5.21"
    implementation "org.slf4j:slf4j-api:2.0.17"

    testImplementation "org.junit.jupiter:junit-jupiter:5.12.1"
}

application {
    mainClass = "com.cubiclauncher.launcher.Launcher"
}

tasks.run {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }

    jvmArgs = [
            "--module-path", configurations.runtimeClasspath.asPath,
            "--add-modules", "javafx.controls,javafx.fxml,javafx.graphics",
            "--enable-native-access=ALL-UNNAMED"
    ]
}

tasks.register('fatJar', Jar) {
    archiveBaseName.set("CubicLauncher")
    archiveClassifier.set(platform + "-" + normalizedArch)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': 'com.cubiclauncher.launcher.Launcher'
        )
    }

    from sourceSets.main.output

    from {
        configurations.runtimeClasspath.collect { zipTree(it) }
    }
}
