/*
 *
 *  * Copyright (C) 2025 Santiagolxx, Notstaff and CubicLauncher contributors
 *  *
 *  * This program is free software: you can redistribute it and/or modify
 *  * it under the terms of the GNU Affero General Public License as published by
 *  * the Free Software Foundation, either version 3 of the License, or
 *  * (at your option) any later version.
 *  *
 *  * This program is distributed in the hope that it will be useful,
 *  * but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  * GNU Affero General Public License for more details.
 *  *
 *  * You should have received a copy of the GNU Affero General Public License
 *  * along with this program.  If not, see https://www.gnu.org/licenses/.
 *
 *
 */

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = "com.cubiclauncher"
version = "1.0-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation "org.openjfx:javafx-controls:26-ea+16"
    implementation "org.openjfx:javafx-fxml:26-ea+16"

    implementation "org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0"
    implementation "com.google.code.gson:gson:2.13.2"

    implementation "com.cubiclauncher:claunch:1.0.0"

    implementation "net.java.dev.jna:jna:5.18.1"
    implementation "net.java.dev.jna:jna-platform:5.18.1"
    implementation("ch.qos.logback:logback-classic:1.5.21")
    implementation("org.slf4j:slf4j-api:2.0.17")

    testImplementation "org.junit.jupiter:junit-jupiter:5.12.1"
}

application {
    mainClass = "com.cubiclauncher.launcher.Launcher"
}

javafx {
    version = "23.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

tasks.run {
    jvmArgs = [
            "--enable-native-access=ALL-UNNAMED",
            "--enable-native-access=javafx.graphics"
    ]
}

tasks.register('fatJar', Jar) {
    archiveBaseName.set("CubicLauncher")
    archiveClassifier.set("all")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE  // ⬅️ Cambiado a EXCLUDE

    manifest {
        attributes(
                'Main-Class': 'com.cubiclauncher.launcher.Launcher'
        )
    }

    from sourceSets.main.output

    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}